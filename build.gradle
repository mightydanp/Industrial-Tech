plugins {
	id 'eclipse'
	id 'idea'
	id 'net.neoforged.gradle.userdev' version '7.0.+'
}

group= project.mod_group_id

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

project.evaluationDependsOn(":TechCore")

runs {	
	configureEach {
		workingDirectory project.file('run')
        systemProperty 'forge.logging.console.level', 'debug'

        modSource project(":TechCore").sourceSets.main
		modSource project.sourceSets.main
	}

	client {
		systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
	}

	server {
		systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
		programArgument '--nogui'
	}

	gameTestServer {
		systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
	}

	data {
		programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
	}
}

repositories {
	maven {
		name = "Jared's maven"
		url = "https://maven.blamejared.com/"
	}
	maven {
		name = "ModMaven"
		url = "https://modmaven.dev"
	}
}

dependencies {
	implementation "net.neoforged:neoforge:${neo_version}"

    implementation project(':TechCore')

	compileOnly "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"
    compileOnly "mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}"
    runtimeOnly "mezz.jei:jei-${minecraft_version}-forge:${jei_version}"
}

modDependencies = [[ modId: "techcore", type: "required", ordering: "AFTER", side: "BOTH", versionRange: "[" + project.tech_core_version + ",)" ]]
modMixins = ["techascension.mixins.json"]

